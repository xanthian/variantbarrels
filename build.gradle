plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url 'https://jitpack.io' }                // Required for Dawn API
	maven { url 'https://maven.terraformersmc.com/' } // Required for Mod Menu and Terraform APIs
	maven { url 'https://maven.shedaniel.me/' }       // Required for Cloth Config
	maven { url 'https://maven.ryanliptak.com/' }     // Required for AppleSkin
	maven { url 'https://maven.minecraftforge.net/' } // Required for TerraBlender
	maven { url "https://maven.resourcefulbees.com/repository/maven-public/" } // Required for Ad Astra
	maven { url "https://cursemaven.com/" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.ladysnake.org/releases"} // Trinkets (for Bewitchment)
	maven { url "https://oss.sonatype.org/content/repositories/snapshots"}
	maven { url "https://maven.jamieswhiteshirt.com/libs-release"}
	maven { url "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"} // Forge Config Api
}
dependencies {
	implementation 'org.jetbrains:annotations:24.0.0'

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Ad Astra  - (issues with Botania - cursed Arch crap - https://legacy.curseforge.com/minecraft/mc-mods/ad-astra)
	//modRuntimeOnly("curse.maven:adastra-635042:4643678") {transitive false}
	//modApi ("curse.maven:botarium-704113:4672474")
	//modApi ("com.teamresourceful.resourcefullib:resourcefullib-fabric-1.20.1:2.1.3")
	//modApi ("curse.maven:resourcefulconfig-714059:4576456")

	// Beach Party (Lets Do)
	modRuntimeOnly("curse.maven:beachparty-858691:4696068")

	// BetterArcheology
	modRuntimeOnly("curse.maven:betterarcheology-835687:4600694") {transitive false}

	// Bewitchment
	modRuntimeOnly("curse.maven:bewitchment-394915:4673611") {transitive false}
	modApi("me.lucko:fabric-permissions-api:0.1-SNAPSHOT")
	modApi("maven.modrinth:impersonate:2.10.0")
	modApi("maven.modrinth:midnightlib:1.4.1-fabric")
	modApi("io.github.ladysnake:PlayerAbilityLib:1.8.0")
	modApi("com.jamieswhiteshirt:reach-entity-attributes:2.4.0")
	modApi("com.github.emilyploszaj:step-height-entity-attribute:25dabcdad7")
	modApi("com.terraformersmc.terraform-api:terraform-wood-api-v1:7.0.1")
	modRuntimeOnly ("vazkii.patchouli:Patchouli:1.20.1-80-FABRIC")
	modRuntimeOnly("com.github.Virtuoel:Pehkui:3.7.6") {transitive false}
	modApi("com.github.Virtuoel:KanosConfig:0.4.1")
	modRuntimeOnly("curse.maven:trinkets-341284:4657534") {transitive false}
	modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.1"
	modApi "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.1"

	// Deeper & Darker - (some sht about CustomPortalBlocks - https://legacy.curseforge.com/minecraft/mc-mods/deeperdarker)
	//modRuntimeOnly("curse.maven:deeperdarker-659011:4715721") {transitive false}

	// Promenade
	modRuntimeOnly("curse.maven:promenade-399648:4611967") {transitive false}
	modRuntimeOnly("com.github.DawnTeamMC:DawnAPI:5.0.0")
	modRuntimeOnly("com.terraformersmc:biolith:1.0.0-alpha.7")

	// Regions Unexplored
	modRuntimeOnly("curse.maven:regions_unexplored-659110:4583966") {transitive false}
	modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.0"

	// Snifferplus - (Datagen issues - ResourceKey[minecraft:worldgen/biome / snifferplus:timeless_grotto - https://legacy.curseforge.com/minecraft/mc-mods/hellions-sniffer)
	//modRuntimeOnly("curse.maven:hellionssiffer-871962:4591532")

	// Tech Reborn
	modRuntimeOnly("TechReborn:TechReborn-1.20:5.8.3") {transitive false }
	modRuntimeOnly("RebornCore:RebornCore-1.20:5.8.3")

	// Vinery
	modRuntimeOnly("curse.maven:vinery-704465:4643442") {transitive false}
	modRuntimeOnly "maven.modrinth:do-api:1.2.4-fabric"
	modApi( "curse.maven:arch-419699:4633047") { exclude group: 'net.fabricmc', module: 'fabric-loader' }
}
loom {
	accessWidenerPath = file("src/main/resources/variantbarrels.accesswidener")

	runs {
		// This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
		datagen {
			inherit server
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=variantbarrels"

			runDir "build/datagen"
		}
	}
}
sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}
processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
