plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

//def apikeyPropertiesFile = rootProject.file("secrets/secrets.properties");
def apikeyProperties = new Properties()
//apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))
repositories {
	mavenCentral()
	maven { url "https://maven.terraformersmc.com/" } // Required for Mod Menu and Terraform APIs
	maven { url "https://maven.shedaniel.me/" }       // Required for Cloth Config
	maven { url "https://maven.ryanliptak.com/" }     // Required for AppleSkin
	maven { url "https://maven.minecraftforge.net/" } // Required for TerraBlender
	maven { url "https://maven.resourcefulbees.com/repository/maven-public/" } // Required for Ad Astra
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // Completecomfig for RegionsUnexplored
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://cursemaven.com/" }
	maven { url "https://api.modrinth.com/maven" }
	maven { url "https://maven.kyrptonaught.dev" }
	maven { url "https://maven.wispforest.io" } // owolib for MineCells
	maven { url "https://maven.draylar.dev/releases" } // omegaconfig for MineCells
	maven { url "https://maven.ladysnake.org/releases" } // Trinkets for Bewitchment
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
	maven { url "https://maven.jamieswhiteshirt.com/libs-release" }
	maven { url "https://maven.parchmentmc.org" }
	maven { url "https://jitpack.io" }
	 //maven {
	 //url = uri("https://maven.pkg.github.com/Lemonszz/taniwha")
	 //credentials {
	 //username = apikeyProperties['gpr.user'] ?: System.getenv("USERNAME")
	 //password = apikeyProperties['gpr.key'] ?: System.getenv("TOKEN")
	 //}
	 //}
}
loom {
	accessWidenerPath = file("src/main/resources/variantbarrels.accesswidener")

	runs {
		datagen {
			inherit server
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
			vmArg "-Dfabric-api.datagen.modid=variantbarrels"

			runDir "build/datagen"
		}
	}
}
sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}

configurations.all {
	resolutionStrategy {
		force("net.fabricmc:fabric-loader:$loader_version")
	}
}

dependencies {
	implementation 'org.jetbrains:annotations:24.0.0'

	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.layered {
		mappings file("mappings/mappings-fix.tiny")
		mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	}
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Ad Astra
	modRuntimeOnly("maven.modrinth:3ufwT9JF:QzjRLT8e")
	modRuntimeOnly("maven.modrinth:2u6LRnMa:9yTYvTAh") //botarium
	modApi("maven.modrinth:G1hIVOrD:5coQdKPg") //resourcefullib
	modApi("maven.modrinth:M1953qlQ:nVDWZ9N7") //resourcefulconfig

	// Beach Party (Lets Do)
	modRuntimeOnly("curse.maven:beachparty-858691:4696068") { transitive false }

	// BetterArcheology
	modRuntimeOnly("curse.maven:betterarcheology-835687:4600694") { transitive false }

	// Bewitchment
	modRuntimeOnly("curse.maven:bewitchment-394915:4673611") { transitive false }
	modApi("me.lucko:fabric-permissions-api:0.1-SNAPSHOT")
	modApi("maven.modrinth:impersonate:2.10.0")
	modApi("maven.modrinth:midnightlib:1.4.1-fabric")
	modApi("io.github.ladysnake:PlayerAbilityLib:1.8.0")
	modApi("com.jamieswhiteshirt:reach-entity-attributes:2.4.0")
	modApi("com.github.emilyploszaj:step-height-entity-attribute:v1.2.0")
	modApi("com.terraformersmc.terraform-api:terraform-wood-api-v1:7.0.1")
	modRuntimeOnly("vazkii.patchouli:Patchouli:1.20.1-80-FABRIC")
	modRuntimeOnly("com.github.Virtuoel:Pehkui:3.7.6") { transitive false }
	modApi("com.github.Virtuoel:KanosConfig:0.4.1")
	modRuntimeOnly("curse.maven:trinkets-341284:4657534") { transitive false }
	modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.1")
	modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.1")

	// BiomeMakeover
	modRuntimeOnly("maven.modrinth:jG8Q2YwT:rsoZ2Iqo") { transitive false }
	//modRuntimeOnly("party.lemons:taniwha-fabric:1.20.0-5.4.9" )

	// Blockus
	modRuntimeOnly("curse.maven:blockus-312289:4745041") { transitive false }

	// Botania
	modRuntimeOnly("vazkii.botania:Botania-xplat:1.20.1-441")

	// Cinderscapes
	modRuntimeOnly "com.terraformersmc:cinderscapes:4.0.9"
	modRuntimeOnly "com.terraformersmc:cinderscapes-client:4.0.9"
	modRuntimeOnly "com.terraformersmc:cinderscapes-common:4.0.9"
	modRuntimeOnly "com.terraformersmc:cinderscapes-worldgen:4.0.9"
	modApi("maven.modrinth:mOgUt4GM:lEkperf6") // ModMenu 7.2.2

	// Deeper & Darker
	modRuntimeOnly("maven.modrinth:fnAffV0n:jb78cjt3") { transitive false }
	modApi("net.kyrptonaught:customportalapi:0.0.1-beta64-1.20")

	// Desolation
	modRuntimeOnly("maven.modrinth:oML3dQBb:rW5RXUV5") { transitive false }
	modRuntimeOnly("maven.modrinth:8BmcQJ2H:oPgOSBzN") { transitive false } // Geckolib

	// Eldritch End
	modRuntimeOnly("curse.maven:eldritch_end-877553:4846136") { transitive false }
	modApi("curse.maven:azure_lib-817423:4829197")
	modApi("curse.maven:necronomicon-586157:4834773")

	// MineCells
	modRuntimeOnly("curse.maven:minecells-618790:4745169") { transitive false }
	modApi("io.wispforest:owo-lib:0.11.2-pre.11+1.20")
	modApi("dev.draylar.omega-config:omega-config-base:1.4.0+1.20.1")

	// Natures Spirit 1.2
	modRuntimeOnly("maven.modrinth:WBvnqHfV:M6a2Rl4r")

	// Promenade
	modRuntimeOnly("curse.maven:promenade-399648:4611967") { transitive false }
	modApi("com.github.DawnTeamMC:DawnAPI:5.0.0")
	modApi("com.terraformersmc:biolith:1.0.0-alpha.7")

	// Regions Unexplored 0.5
	modRuntimeOnly("curse.maven:regions_unexplored-659110:4721079")
	modApi("com.github.glitchfiend:TerraBlender-fabric:1.20.1-3.0.0.169")
	modApi("com.github.Lortseam.completeconfig:base:2.5.0")

	// Snifferplus
	//modRuntimeOnly("maven.modrinth:BVgHoKxg:DLjYM0KE")

	// Tech Reborn
	modRuntimeOnly("TechReborn:TechReborn-1.20:5.8.3") { transitive false }
	modRuntimeOnly("RebornCore:RebornCore-1.20:5.8.3")

	// Vinery
	modRuntimeOnly("curse.maven:vinery-704465:4643442") { transitive false }
	modRuntimeOnly("maven.modrinth:do-api:1.2.4-fabric") { transitive false }
	modApi("curse.maven:arch-419699:4633047") { exclude group: 'net.fabricmc', module: 'fabric-loader' }

	// Mixin Extras
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0")))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}
